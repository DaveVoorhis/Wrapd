plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.reldb'
version = '1.0.0'
description = 'Wrapd SQL Amplifier'
java.sourceCompatibility = JavaVersion.VERSION_11

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign publishing.publications
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'org.reldb:Toolbox:1.0.0'
}

dependencies {
    testImplementation 'org.eclipse.jdt:ecj:3.27.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.junit.platform:junit-platform-runner:1.8.1'
    testImplementation 'com.mchange:c3p0:0.9.5.5'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    testImplementation 'org.postgresql:postgresql:42.2.23'
    testImplementation 'org.xerial:sqlite-jdbc:3.36.0.2'
    testImplementation 'mysql:mysql-connector-java:8.0.25'
}

test {
    useJUnitPlatform()
}

clean {
    delete '_TestData'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/wrapd.version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "Wrapd"
                description = "Wrapd is a SQL amplifier, a lightweight database abstraction layer generator and schema migrator that helpfully exposes SQL in Java rather than hiding it."
                url = "https://wrapd.org"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "DaveVoorhis"
                        name = "Dave Voorhis"
                        email = "voorhis@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/DaveVoorhis/Wrapd.git"
                    developerConnection = "scm:git:https://github.com/DaveVoorhis/Wrapd.git"
                    url = "https://wrapd.org"
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

javadoc {
    options.overview = "src/javadoc/overview.html"

    doLast {
        delete "docs/javadoc"
        copy {
            from "build/docs/"
            into "docs/"
        }
    }
}