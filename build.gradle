plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.reldb'
version = '1.0.3-SNAPSHOT'
description = 'Wrapd SQL Amplifier'

java.sourceCompatibility = JavaVersion.VERSION_11

dependencies {
    api 'org.reldb:Toolbox:1.0.0'
    implementation 'org.yaml:snakeyaml:1.30'
    testImplementation 'org.eclipse.jdt:ecj:3.29.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.junit.platform:junit-platform-runner:1.8.2'
    testImplementation 'com.mchange:c3p0:0.9.5.5'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.17.2'
    testRuntimeOnly 'org.postgresql:postgresql:42.3.3'
    testRuntimeOnly 'org.xerial:sqlite-jdbc:3.36.0.3'
    testRuntimeOnly 'mysql:mysql-connector-java:8.0.28'
    testRuntimeOnly 'com.microsoft.sqlserver:mssql-jdbc:9.4.1.jre11'
}

test {
    useJUnitPlatform()
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/wrapd.version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

javadoc {
    options.overview = "src/javadoc/overview.html"
}

clean {
    delete '_TestData'
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

/**
 * This build.gradle will publish to the oss Nexus repository manager
 * at https://s01.oss.sonatype.org for staging to Maven Central.
 *
 * For that to work, you need a .gradle/gradle.properties file
 * with appropriate values for:
 *
 *   mavenCentralUsername= ...
 *   mavenCentralPassword= ...
 *   signing.keyId= ...
 *   signing.password= ...
 *   signing.secretKeyRingFile= ...
 *
 * The mavenCentral* values are used by publishing.
 * The signing.* values are used by signing.
 */

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    sign publishing.publications
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "Wrapd"
                description = "Wrapd is a SQL amplifier, a lightweight database abstraction layer generator and schema migrator that helpfully exposes SQL in Java rather than hiding it."
                url = "https://wrapd.org"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "DaveVoorhis"
                        name = "Dave Voorhis"
                        email = "voorhis@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/DaveVoorhis/Wrapd.git"
                    developerConnection = "scm:git:https://github.com/DaveVoorhis/Wrapd.git"
                    url = "https://wrapd.org"
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('mavenCentralUsername') ? mavenCentralUsername : "no_user"
                password = project.hasProperty('mavenCentralPassword') ? mavenCentralPassword : "no_password"
            }
        }
    }
}
