plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.reldb'
version = '0.0.5-SNAPSHOT'
description = 'Wrapd SQL Amplifier'
java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'com.mchange:c3p0:0.9.5.5'
    implementation 'org.eclipse.jdt:ecj:3.26.0'
}

// only for WrapDemo
dependencies {
    implementation 'mysql:mysql-connector-java:8.0.25'
    implementation files("./_TestData/WrapDemo/code")
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.junit.platform:junit-platform-runner:1.7.2'
    testImplementation 'org.postgresql:postgresql:42.2.23'
    testImplementation 'org.xerial:sqlite-jdbc:3.36.0.1'
    testImplementation 'mysql:mysql-connector-java:8.0.25'
}

test {
    useJUnitPlatform()
}

clean {
    delete '_TestData'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

javadoc {
    options.overview = "src/javadoc/overview.html"
}