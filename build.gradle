plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.reldb'
version = '0.0.7-SNAPSHOT'
description = 'Wrapd SQL Amplifier'
java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.eclipse.jdt:ecj:3.26.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.junit.platform:junit-platform-runner:1.7.2'
    testImplementation 'com.mchange:c3p0:0.9.5.5'
    testImplementation 'org.postgresql:postgresql:42.2.23'
    testImplementation 'org.xerial:sqlite-jdbc:3.36.0.2'
    testImplementation 'mysql:mysql-connector-java:8.0.25'
}

test {
    useJUnitPlatform()
}

clean {
    delete '_TestData'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

javadoc {
    options.overview = "src/javadoc/overview.html"

    doLast {
        delete "docs/javadoc"
        copy {
            from "build/docs/"
            into "docs/"
        }
    }
}